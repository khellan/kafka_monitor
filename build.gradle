defaultTasks 'build'

apply plugin: 'java'

version = '0.1'

configurations {
    provided
    compile.extendsFrom provided
    runtime.extendsFrom compile
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Zedge Event Monitor',
                   'Implementation-Version': version
    }
}

ext {
    versions = [:]
}

versions += [
        scala: "2.10",
        spark: "1.2.0",
        cloudera: "cdh5.3.3"
]

repositories {
    mavenCentral()
    maven { url "https://repository.cloudera.com/artifactory/cloudera-repos/" }
}

List spark = [
    "org.apache.spark:spark-core_$versions.scala:$versions.spark-$versions.cloudera",
    "org.apache.spark:spark-streaming_$versions.scala:$versions.spark-$versions.cloudera",
]

List netty = [
    "org.jboss.netty:netty:3.2.2.Final",
    "io.netty:netty:3.6.6.Final",
]

dependencies {
    provided spark
    provided netty
    compile group: 'com.google.guava', name: 'guava', version: '12.0'
    compile group: 'org.apache.spark', name: "spark-streaming-kafka_$versions.scala", version: "$versions.spark-$versions.cloudera"
}

jar {
    dependsOn configurations.runtime
    from {
        (configurations.runtime - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}